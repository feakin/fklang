program     = _{ SOI ~ expr ~ EOI }

stmt        = _{ assign | expr }
assign      = { ident ~ assign_op ~ expr }
expr        = { comparison }
comparison  = { elvis_expr ~ (equal_op ~ elvis_expr)* }
elvis_expr  = { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }
primary     = _{ function | variable | num | "(" ~ expr ~ ")" }
infix       = _{ sub | add | mul | div | pow | modulus }
prefix      = _{ neg }
postfix     = _{ fac }

add         = { "+" | "with" | "plus" | "add" }
sub         = { "-" | "without" | "subtract" | "minus" }
mul         = { "*" | "times" | "multiply by" | "mul" }
div         = { "/" | "divide by" | "divide" }
pow         = { "^" | "power" }
modulus     = { "%" | "mod" }

neg         = { "-" } // Negation
fac         = { "!" } // Factorial

equal_op    = { equal | not_equal }
equal       = _{ "==" }
not_equal   = _{ "!=" }

assign_op   = { ":=" }

function = { ident ~ "(" ~ expr ~ ("," ~ expr)* ~ ")" }

variable = _{ ident }

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* ~ "'"* }
int = { ("+" | "-")? ~ ASCII_DIGIT+ }
num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }

WHITESPACE   =  _{ " " | "\t" | NEWLINE }
